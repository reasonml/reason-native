/**
 * Copyright (c) Facebook, Inc. and its affiliates.
 *
 * This source code is licensed under the MIT license found in the
 * LICENSE file in the root directory of this source tree.
 */;

let cli = (argv, testSuites) => {
  CLI.(
    program("Rely")
    |> description("A jest inspired native Reason testing framework")
    |> option(
         "--updateSnapshots",
         ~aliases=["-u"],
         "Updates snapshots to match what is currently generated by the program",
         Optional(Bool),
       )
    |> option(
         "--onlyPrintDetailsForFailedSuites",
         "Prints output only for test suites that fail",
         Optional(Bool),
       )
    |> option(
         "--ci",
         ~aliases=["-ci"],
         "Runs Rely in CI mode (errors on usage of testOnly and describeOnly)",
         Optional(Bool),
       )
    |> argument(
         "testNamePattern",
         "Only run tests matching the passed in pattern",
         Optional(String),
       )
    |> action(({options, args}) => {
         let shouldUpdateSnapshots = options.bool("--updateSnapshots");
         let ci = options.bool("--ci");
         let onlyPrintDetailsForFailedSuites =
           options.bool("--onlyPrintDetailsForFailedSuites");

         let config =
           RunConfig.(
             initialize()
             |> updateSnapshots(shouldUpdateSnapshots)
             |> ciMode(ci)
             |> withTestNamePattern(
                  args.optionalString("testNamePattern"),
                )
             |> withReporters([
                  Custom(
                    TerminalReporter.createTerminalReporter(
                      ~onlyPrintDetailsForFailedSuites,
                      {
                        printEndline: print_endline,
                        printNewline: print_newline,
                        printString: print_string,
                        flush,
                      },
                    ),
                  ),
                ])
           );
         TestSuiteRunner.run(config, testSuites);
       })
    |> parseAndRun(argv)
  );
};
